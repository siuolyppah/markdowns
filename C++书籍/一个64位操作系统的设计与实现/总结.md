# 从加电开始的流程

1. 系统加电，CPU的cs:ip寄存器被强制初始化0xF000:0xFFF0；

   > - 此时CPU处于实模式下，只能访问20条地址线，而每个寄存器都是16位，因此物理地址=段基址左移4位+段内偏移地址。
   >
   > - cs寄存器即段寄存器，ip寄存器即段偏移寄存器，其等效地址为0xFFFF0，此地址即为BIOS的入口程序。
   > - BIOS存储在ROM中，该ROM被映射在低端1MB内存的顶部，即地址0xF0000~0xFFFFF处。
   >
   > ![image-20220909215238560](%E6%80%BB%E7%BB%93.assets/image-20220909215238560.png)

2. 执行BIOS程序，并检测内存、显卡等外设信息。当检测通过，并初始化好设备后，开始在内存0x000~0x3FF处建立中断向量表并填写中断例程；

3. BIOS自检结束后，根据启动选项设置，选择启动设备。即检测软盘的第0磁头第0磁道第1扇区，是否以0x55和0xaa两字节为结尾。若是，则BIOS认为该扇区是一个Boot Sector（引导扇区），并将该扇区的数据，复制到物理地址0x7c00处，并跳转至该地址执行；

   > - 引导扇区内存放的程序，即为主引导记录（MBR）
   > - BIOS跳转到0x7c00，使用的`jmp 0：0x7c00`

   <img src="%E6%80%BB%E7%BB%93.assets/epub_26211970_12.jpeg" alt="epub_26211970_12" style="zoom:33%;" />



## MBR

```asm
; 由BIOS通过使用jmp 0:0x7c00跳转至此程序
; 此程序即为MBR

org	0x7c00	

BaseOfStack	equ	0x7c00

; Label_Start即代表了一条地址，就是下面的mov指令的地址
Label_Start:
;= cs当前为0,ip为0x7c00
	mov	ax,	cs
	mov	ds,	ax
	mov	es,	ax
	mov	ss,	ax
	mov	sp,	BaseOfStack

;=======	clear screen

	mov	ax,	0600h
	mov	bx,	0700h
	mov	cx,	0
	mov	dx,	0184fh
	int	10h

;=======	set focus

	mov	ax,	0200h
	mov	bx,	0000h
	mov	dx,	0000h
	int	10h

;=======	display on screen : Start Booting......

	mov	ax,	1301h
	mov	bx,	000fh
	mov	dx,	0000h
	mov	cx,	10
	push	ax
	mov	ax,	ds
	mov	es,	ax
	pop	ax
	mov	bp,	StartBootMessage
	int	10h

;=======	reset floppy

	xor	ah,	ah
	xor	dl,	dl
	int	13h

	jmp	$

StartBootMessage:	db	"Start Boot"
;=======	fill zero until whole sector
	times	510 - ($ - $$)	db	0
	dw	0xaa55

```

或者是：

````asm
;主引导程序 
;------------------------------------------------------------
SECTION MBR vstart=0x7c00         
   mov ax,cs      
   mov ds,ax
   mov es,ax
   mov ss,ax
   mov fs,ax
   mov sp,0x7c00
   

; 清屏 利用0x06号功能，上卷全部行，则可清屏。
; -----------------------------------------------------------
;INT 0x10   功能号:0x06	   功能描述:上卷窗口
;------------------------------------------------------
;输入：
;AH 功能号= 0x06
;AL = 上卷的行数(如果为0,表示全部)
;BH = 上卷行属性
;(CL,CH) = 窗口左上角的(X,Y)位置
;(DL,DH) = 窗口右下角的(X,Y)位置
;无返回值：
   mov     ax, 0x600
   mov     bx, 0x700
   mov     cx, 0           ; 左上角: (0, 0)
   mov     dx, 0x184f	   ; 右下角: (80,25),
			   ; VGA文本模式中,一行只能容纳80个字符,共25行。
			   ; 下标从0开始,所以0x18=24,0x4f=79
   int     0x10            ; int 0x10

;;;;;;;;;    下面这三行代码是获取光标位置    ;;;;;;;;;
;.get_cursor获取当前光标位置,在光标位置处打印字符.
   mov ah, 3		; 输入: 3号子功能是获取光标位置,需要存入ah寄存器
   mov bh, 0		; bh寄存器存储的是待获取光标的页号

   int 0x10		; 输出: ch=光标开始行,cl=光标结束行
			; dh=光标所在行号,dl=光标所在列号

;;;;;;;;;    获取光标位置结束    ;;;;;;;;;;;;;;;;

;;;;;;;;;     打印字符串    ;;;;;;;;;;;
   ;还是用10h中断,不过这次是调用13号子功能打印字符串
   mov ax, message 
   mov bp, ax		; es:bp 为串首地址, es此时同cs一致，
			; 开头时已经为sreg初始化

   ; 光标位置要用到dx寄存器中内容,cx中的光标位置可忽略
   mov cx, 5		; cx 为串长度,不包括结束符0的字符个数
   mov ax, 0x1301	; 子功能号13是显示字符及属性,要存入ah寄存器,
			; al设置写字符方式 ah=01: 显示字符串,光标跟随移动
   mov bx, 0x2		; bh存储要显示的页号,此处是第0页,
			; bl中是字符属性, 属性黑底绿字(bl = 02h)
   int 0x10		; 执行BIOS 0x10 号中断
;;;;;;;;;      打字字符串结束	 ;;;;;;;;;;;;;;;

   jmp $		; 使程序悬停在此

   message db "1 MBR"
   times 510-($-$$) db 0
   db 0x55,0xaa

````





# 汇编知识

## 标号

```asm
code_start:
	mov ax,0
```

其中，`code_start`这个标号被nasm认为是一个地址，该地址便是下面这条mov指令的地址。

> code_start只是一个标记，与伪指令类似，CPU并不识别。
>
> nasm会使用其安排的地址，来替换该标号的出现。



## \$

\$属于“隐式地”藏在本行代码前的标号，也就是编译器给当前行安排的地址，看不到却又无处不在，\$在每行都有。或者这种说法并不是很正确，只有“显示地”用了\$的地方， nasm 编译器才会将此行的地
址公布出来。  

```asm
code_start:
	jmp $
```

在此处，$和code_start是一个值，都是此jmp指令所在的地址。



## \$\$

\$\$指代本 section 的起始地址，此地址同样是编译器给安排的 。  

- 默认情况下，它们的值是相对于本文件开头的偏移量。
- 至于实际安排的是多少，还要看程序员同学是否在 section 中添加了 vstart。这个关键字可以影响编译器安排地址的行为，如果该 section 用了 vstart=xxxx 修饰，$$的值则是此 section 的虚拟起始地址。



如果没有定义 section, nasm默认全部代码同为一个 section，起始地址为 0。

> section 是伪指令， CPU 运行程序是不需要这个东西的，这个只是用来给程序员规划程序用的，有了 section，就可以将自己的代码分成一段一段的， 当然这只是在逻辑上的段，实际上编译出来的程序可能还是完整的一体。  





# 一些命令

## nasm

```sh
nasm -f <format><filename> [-o <output>]
```

-f用于指定文件格式，使用-hf可以查看所有有效的文件格式：

>- bin是默认输出格式，指的是纯二进制文件。
>- 如elf或pe格式的二进制可执行文件，参杂了许多和指令无关的东西，包含程序的内存布局、位置等信息，这些信息是给操作系统的程序加载器使用的，属于操作系统规划的范畴。

![image-20220909230027764](%E6%80%BB%E7%BB%93.assets/image-20220909230027764.png)